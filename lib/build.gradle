
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'it.scoppelletti.spaceship.android-library'
    id 'org.jetbrains.dokka'
}

group 'it.scoppelletti.spaceship'
version '1.1.0'
description 'Spaceship for HTML'
archivesBaseName = 'spaceship-html'

android {
    namespace 'it.scoppelletti.spaceship.html'
    compileSdk sdk_target
    defaultConfig {
        minSdk sdk_min
        targetSdk sdk_target
        vectorDrawables.useSupportLibrary = true
        aarMetadata {
            minCompileSdk = sdk_min
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.internal.KaptGenerateStubsTask).configureEach {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-html'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-html.git'
    inceptionYear = '2018-2021'
}

configurations {
    bom
    api.extendsFrom(bom)
    kapt.extendsFrom(bom)
    coreLibraryDesugaring.extendsFrom(bom)
}

dependencies {
    bom platform(libs.bom.kotlin)
    bom platform(libs.bom.spaceship)
    api 'androidx.activity:activity-ktx'
    api 'androidx.annotation:annotation'
    api 'androidx.appcompat:appcompat'
    api 'androidx.browser:browser'
    api 'androidx.fragment:fragment-ktx'
    api 'androidx.webkit:webkit'
    api 'com.google.android.material:material'
    api 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    api 'it.scoppelletti.spaceship:spaceship'
    api 'it.scoppelletti.spaceship:spaceship-stdlib'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'org.slf4j:slf4j-api'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs'
}
